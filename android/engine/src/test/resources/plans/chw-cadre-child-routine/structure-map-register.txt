map "http://hl7.org/fhir/StructureMap/39bfbe34-40f2-11ee-be56-0242ac120002" = 'Child routine visit'

uses "http://hl7.org/fhir/StructureDefinition/Parameters" as source
uses "http://hl7.org/fhir/StructureDefinition/CarePlan" as target

group ChildRoutineVisit(source src : Parameters, target carePlan: CarePlan) {
    src -> evaluate(src, $this.parameter.where(name='subject').resource) as subject,
            evaluate(src, $this.parameter.where(name='definition').resource) as definition
            then ExtractCarePlan(subject, definition, carePlan), ExtractActivityDetail(subject, definition, carePlan) "r_careplan";
}

group ExtractCarePlan(source child: Patient, source definition: ActivityDefinition, target careplan : CarePlan) {
    child -> careplan.id = uuid() ,
           careplan.identifier = create('Identifier') as identifier, identifier.value = uuid(), identifier.use = 'official',
           careplan.status = 'active',
           careplan.intent = 'plan',
           careplan.subject = create('Reference') as ref, ref.reference = evaluate(child, $this.id.replaceMatches('/_history/.*', '')),
           careplan.created = evaluate(child, now()),
           careplan.author = evaluate(child, $this.generalPractitioner.first()),
           careplan.period = create('Period') as period, evaluate(child, today()) as offset then ExtractPeriod_30d(offset, period) "r_cp_data";
}

group ExtractActivityDetail(source child : Patient, source definition: ActivityDefinition, target carePlan : CarePlan){
    child -> carePlan.activity = create('CarePlan_Activity') as activity then {
        child -> activity.detail = create('CarePlan_ActivityDetail') as det then {
            child -> det.kind = 'Task',
                   det.status = 'in-progress',
                   det.description = evaluate(definition, $this.title),
                   det.performer = evaluate(child, $this.generalPractitioner.first()),
                   det.code = create('CodeableConcept') as concept then ExtractTimingCode(child, concept) "r_act_det_data";

            child -> det.scheduled = evaluate(definition, $this.timing) as timing,
                   evaluate(timing, $this.repeat) as repeat then {

                child -> evaluate(child, today() + '30 \'days\''.toQuantity()) as dueDate
                   then ExtractTasks(dueDate, repeat, child, carePlan, activity, timing) "r_tasks";

                child -> repeat.count = create('positiveInt') as c, c.value = evaluate(activity, $this.outcomeReference.count().value) "r_task_rep_count";

            } "r_tim_repeat";
        } "r_act_det";

    } "r_cp_acti";
}

group ExtractTasks(source dueDate: DateType, source repeat: TimingRepeat, source child : Patient, target carePlan: CarePlan, target activity : CarePlan_Activity,target timing: Timing){

    repeat where( (today() + (((countMax.toDecimal() + 30) * period).toString()+' \'days\'').toQuantity()) >= dueDate) -> dueDate as start,
                evaluate(carePlan, $this.period.end) as end,
                create('Period') as period,
                carePlan.contained = create('Task') as task then {

        child then ExtractPeriod(start, end, period) "r_task_period_extr";

        child -> task.id = uuid(),
               task.identifier = create('Identifier') as iden, iden.value = uuid(), iden.use = 'official',
               task.status = 'requested',
               task.intent = 'plan',
               task.executionPeriod = period,
               task.priority = 'routine',
               task.description = 'Sick Child Follow Up',
               task.for = create('Reference') as ref, ref.reference = evaluate(subject, $this.id.replaceMatches('/_history/.*', '')),
               task.basedOn = reference(carePlan),
               task.authoredOn = evaluate(child, now()),
               task.requester = evaluate(child, $this.generalPractitioner.first()),
               task.owner = evaluate(child, $this.generalPractitioner.first()) "r_task_data";
  //Create an immunization follow up Task
        child -> task.reasonReference = create('Reference') as ref, ref.reference = 'Questionnaire/1bdf21ef-d31a-446e-aaa6-cec516eceab1' "r_task_reason_ref";

        child -> activity.outcomeReference = reference(task) "r_cp_task_ref";
        child -> timing.event = evaluate(period, $this.start) "r_activity_timing";

        repeat -> evaluate(period, $this.start + (repeat.period.toString() + ' \'days\'').toQuantity()) as nextDueDate
                  then ExtractTasks(nextDueDate, repeat, child, carePlan, activity, timing) "r_task_repeat";
    } "r_cp_acti_outcome";
}

group ExtractTimingCode(source child : Patient, target concept: CodeableConcept){
    child -> concept.coding = c("http://terminology.hl7.org/CodeSystem/v3-GTSAbbreviation", "QD") as coding then {
        child -> coding.display = 'QD' "r_cp_cod_disp";
    } "r_cp_cc_cod";
    child -> concept.text = 'QD' "r_cp_cc_txt";
}

group ExtractPeriod_30d(source offset: DateType, target period: Period){
    offset -> offset as start,
              evaluate(offset, $this + '30 \'days\''.toQuantity()) as end then
              ExtractPeriod(start, end, period) "r_period";
}

group ExtractPeriod(source start: DateType, source end: DateType, target period: Period) {
    start -> period.start = create('dateTime') as dt,
             dt.value = evaluate(start, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_start";

    end -> period.end = create('dateTime') as dt,
           dt.value = evaluate(end, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_end";
}