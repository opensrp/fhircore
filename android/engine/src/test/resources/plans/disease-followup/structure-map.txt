map "https://fhir.labs.smartregister.org/fhir/StructureMap/63752b18-9f0e-48a7-9a21-d3714be6309a" = "eCBIS Disease Followup"
uses "http://hl7.org/fhir/StructureDefinition/Parameters" as source
uses "http://hl7.org/fhir/StructureDefinition/CarePlan" as target
group eCBISNewPregnancyRegistrationAndVisit(source src : Parameters, target tgt: CarePlan) {
    src -> evaluate(src, $this.parameter.where(name='subject').resource) as subject,
            evaluate(src, $this.parameter.where(name='definition').resource) as definition,
            evaluate(src, $this.parameter.where(name='depends-on').resource.entry.where(resource is QuestionnaireResponse).resource) as questionnaireResponse
            then ExtractCarePlan(subject, definition, questionnaireResponse, tgt), ExtractActivityDetail(subject, definition, questionnaireResponse, tgt) "r_careplan";
}
group ExtractCarePlan(source subject: Patient, source definition: ActivityDefinition, source questionnaireResponse: QuestionnaireResponse, target careplan : CarePlan) {
    subject -> careplan.id = uuid() ,
            careplan.identifier = create('Identifier') as iden, iden.value = uuid(), iden.use = 'official',
            careplan.status = 'active',
            careplan.intent = 'plan',
            careplan.subject = create('Reference') as ref, ref.reference = evaluate(subject, $this.id.replaceMatches('/_history/.*', '')),
            careplan.created = evaluate(subject, now()),
            careplan.author = evaluate(subject, $this.generalPractitioner.first()) "r_cp_data";
}
group ExtractActivityDetail(source subject : Patient, source definition: ActivityDefinition, source src: QuestionnaireResponse, target careplan : CarePlan){
    subject -> careplan.activity = create('CarePlan_Activity') as activity then {
        subject -> activity.detail = create('CarePlan_ActivityDetail') as det then {
            subject -> evaluate(src, src.descendants().where(linkId='f8fdb1c1-fff2-49f9-8f93-8a4c1f55c9ce').answer.value.count()) as answerItems then {
            subject -> det.kind = 'Task',
                   det.status = 'in-progress',
                   det.description = evaluate(definition, $this.title),
                   det.performer = evaluate(subject, $this.generalPractitioner.first()),
                   subject then ExtractDiseaseCode(src, det) "r_act_det_data";
            subject -> det.scheduled = evaluate(definition, $this.timing) as timing,
                   evaluate(timing, $this.repeat) as repeat then {
                subject -> evaluate(subject, ((repeat.count.toString().toInteger() - 1).toString() + ' months').toQuantity()) as duration,
                    duration.code = 'months',
                    evaluate(subject, today()) as dueDate, evaluate(subject, today() + duration) as maxDate
                   then ExtractTasks(dueDate, maxDate, repeat, subject, careplan, activity, timing) "r_tasks";
                subject -> repeat.count = create('positiveInt') as c, c.value = evaluate(activity, $this.outcomeReference.count().value) "r_task_rep_count";
            } "r_tim_repeat";
            } "r_rrr";
        } "r_act_det";
    } "r_cp_acti";
}
group ExtractTasks(
    source dueDate: DateType,
    source maxDate: DateType,
    source repeat: TimingRepeat,
    source subject : Patient,
    target careplan: CarePlan,
    target activity : CarePlan_Activity,
    target timing: Timing){
    // today + total * gap
    repeat where(dueDate <= maxDate) ->
                // start of task is today OR first date of every month if future month | end is last day of given month
                create('date') as startOfMonth, startOfMonth.value = evaluate(dueDate, $this.value.substring(0,7) + '-01'),
                create('date') as start, start.value = evaluate(dueDate, iif($this = today(), $this, startOfMonth).value ),
                evaluate(startOfMonth, '1 month'.toQuantity()) as duration1month, duration1month.code = 'month',
                evaluate(startOfMonth, ($this + duration1month) - '1 day'.toQuantity()) as end,
                create('Period') as period,
                careplan.contained = create('Task') as task then {
        subject then ExtractPeriod(start, end, period) "r_task_period_extr";
        subject -> task.id = uuid(),
               task.identifier = create('Identifier') as iden, iden.value = uuid(), iden.use = 'official',
               task.status = 'requested',
               task.intent = 'plan',
               task.executionPeriod = period,
               task.priority = 'routine',
               task.description = 'Disease Follow Up Task',
               task.for = create('Reference') as ref, ref.reference = evaluate(subject, $this.id.replaceMatches('/_history/.*', '')),
               task.basedOn = reference(careplan),
               task.authoredOn = evaluate(subject, now()),
               task.requester = evaluate(subject, $this.generalPractitioner.first()),
               task.owner = evaluate(subject, $this.generalPractitioner.first()) "r_task_data";
        subject -> task.reasonReference = create('Reference') as ref, ref.reference = 'Questionnaire/e14b5743-0a06-4ab5-aaee-ac158d4cb64f' "r_task_reason_ref";
        subject -> activity.outcomeReference = reference(task) "r_cp_task_ref";
        subject -> timing.event = evaluate(period, $this.start) "r_activity_timing";
        repeat -> evaluate(period, (repeat.period.toString() + ' months').toQuantity()) as duration,
                   duration.code = 'months',
                   evaluate(period, $this.start + duration) as nextDueDate
                          then ExtractTasks(nextDueDate, maxDate, repeat, subject, careplan, activity, timing) "r_task_repeat";
    } "r_cp_acti_outcome";
}
group ExtractPeriod(source start: DateType, source end: DateType, target period: Period) {
    start -> period.start = create('dateTime') as dt,
             dt.value = evaluate(start, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_start";
    end -> period.end = create('dateTime') as dt,
           dt.value = evaluate(end, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_end";
}
group ExtractDiseaseCode(source src: QuestionnaireResponse, target det: ActivityDetail) {
    src.item as dis_item where(src.descendants().where(linkId='f8fdb1c1-fff2-49f9-8f93-8a4c1f55c9ce')) then {
        dis_item.answer as hiv where(value.code='hiv') -> evaluate(hiv, value) as hivcode then ExtractCodeableConcept(src, hivcode, det) "r_det_c";
        dis_item.answer as tb where(value.code='tb') -> evaluate(tb, value) as tbcode then ExtractCodeableConcept(src, tbcode, det) "r_det_c";
        dis_item.answer as me where(value.code='mental-health') -> evaluate(me, value) as mecode then ExtractCodeableConcept(src, mecode, det) "r_det_c";

        dis_item.answer as ntd where(value.code='cm-ntd') -> evaluate(ntd, value) as dp then {
            ntd.item as nvc where(src.descendants().where(linkId='605bd5c8-f279-4903-876f-5e7920d80c32')) then {
                nvc.answer as bu where(value.code='buruli-ulcer') -> evaluate(bu, value) as bucode, evaluate(src, "CM-NTD Buruli Ulcer") as displayText then ExtractCoding(src, bucode, displayText, det) "r_det_c";
                nvc.answer as lep where(value.code='leprosy') -> evaluate(lep, value) as lepcode, evaluate(src, "CM-NTD Leprosy") as displayText then ExtractCoding(src, lepcode, displayText, det) "r_det_c";
                nvc.answer as lf where(value.code='lymphatic-filariasis') -> evaluate(lf, value) as lfcode, evaluate(src, "CM-NTD Lymphatic Filariasis") as displayText then ExtractCoding(src, lfcode, displayText, det) "r_det_c";
            } "r_det_n_cc";
        } "r_det_cc";
    } "r_det_c";
}
group ExtractCodeableConcept(source src: QuestionnaireResponse, source icoding: Coding, target det: ActivityDetail) {
    src -> det.code = create("CodeableConcept") as ccDisease then {
        src -> ccDisease.coding = icoding "r_det_code_coding";
        src -> ccDisease.text = evaluate(icoding, $this.display) "r_det_code_text";
    } "r_det_cc";
}
group ExtractCoding(source src: QuestionnaireResponse, source code: String, source displayText: String, target det: ActivityDetail) {
    src -> det.code = create("CodeableConcept") as ccDisease then {
        src -> ccDisease.coding = c("http://snomed.info/sct", code, displayText) as coding, coding.display = displayText "r_det_code_coding";
        src -> ccDisease.text = displayText "r_det_code_text";
    } "r_det_cc";
}