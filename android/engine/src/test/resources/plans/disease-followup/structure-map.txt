map "http://hl7.org/fhir/StructureMap/0863635d-c78f-489c-9ffa-e49e8b58f576" = 'Measles Complete Vaccine workflow'

uses "http://hl7.org/fhir/StructureDefinition/Parameters" as source
uses "http://hl7.org/fhir/StructureDefinition/CarePlan" as target

group childImmunizationSchedule(source src : Parameters, target careplan: CarePlan) {
    src -> evaluate(src, $this.parameter.where(name='subject').resource) as subject,
        evaluate(src, $this.parameter.where(name='definition').resource) as definition,
        evaluate(src, $this.parameter.where(name='depends-on').resource.entry.where(resource is QuestionnaireResponse).resource) as questionnaireResponse,
        evaluate(src, $this.parameter.where(name='period').value) as period,
        evaluate(src, $this.parameter.where(name='version').value) as version
        then extractImmunization(period, version, subject, definition, questionnaireResponse, careplan) "r_careplan";
}

group extractImmunization(
    source period: Period,
    source version: Integer,
    source subject : Patient,
    source definition: ActivityDefinition,
    source questionnaireResponse: QuestionnaireResponse,
    target careplan: CarePlan) {

    // create activity.detail of type/kind Task for this Task if not exists
    subject where(careplan.activity.where(detail.kind = 'MedicationRequest').exists().not()) -> careplan.activity = create('CarePlan_Activity') as activity then {
        subject -> activity.detail = create('CarePlan_ActivityDetail') as ActivityDefinition then {
            subject -> ActivityDefinition.kind = 'MedicationRequest' "rule_activity_detail_data";
        } "rule_activity_detail";
    } "rule_careplan_activity";

    questionnaireResponse.item as questionnaireResponseItem where(linkId = '5b303e22-5ba0-4d69-967a-0c37f478ad03' and answer.value.exists()) then {
        questionnaireResponse.item as questionnaireResponseItem where(linkId = '1e1f0f3b-58de-41b4-8db0-6c4b033cce57' and answer.exists()) then {

            questionnaireResponse.item as questionnaireResponseItem where(linkId = '5b303e22-5ba0-4d69-967a-0c37f478ad03' and answer.where(value.code = 'yes').exists()) then {
                subject then generateImmunization(period, version, subject, definition, questionnaireResponse, careplan) "r_medication_request_consent_stock";
                subject then updateMedicationRequest(period, version, subject, definition, questionnaireResponse, careplan) "r_medication_request_stock";
            } "questionnaire_response_item_check_consent";

            questionnaireResponse.item as questionnaireResponseItem where(linkId = '5b303e22-5ba0-4d69-967a-0c37f478ad03' and answer.where(value.code = 'no').exists()) then {
                subject then extractMedicationRequestPlanOnHold(period, version, subject, definition, questionnaireResponse, careplan) "r_medication_request_consent";
            } "questionnaire_response_item_consent_no";
        } "questionnaire_response_item_answer_two_available";
    } "questionnaire_response_item_answer_available";
}

group generateImmunization(
    source period: Period,
    source version: Integer,
    source subject : Patient,
    source definition: ActivityDefinition,
    source questionnaireResponse: QuestionnaireResponse,
    target careplan: CarePlan) {

    subject -> create('Immunization') as immunization then {
        subject -> immunization.id = uuid() "rule_immunization_id";
        subject -> immunization.status = 'completed' "rule_immunization_status";
        subject -> immunization.vaccineCode = evaluate(definition, $this.product) "immunization_vaccineCode";
        subject -> immunization.vaccineCode = create('CodeableConcept') as reasonCode then {
            subject -> reasonCode.coding = c("http://snomed.info/sct", "33879002") as reasonCodeCoding then {
                subject -> reasonCodeCoding.display = "Administration of vaccine to produce active immunity (procedure)" "rule_immunization_reasonCode_coding_display";
            } "rule_immunization_reasonCode_coding";
            subject -> reasonCode.text = "Administration of vaccine to produce active immunity (procedure)" "rule_immunization_reasonCode_text";
        } "rule_immunization_reasonCode";

        subject -> immunization.performer = create('Immunization_Performer') as performer then {
            subject -> performer.actor = evaluate(subject, $this.generalPractitioner.first()) "rule_immunization_performer_actor";
        } "rule_immunization_performer";

        //adding the dosage number from QR to the immunization protocol applied[x].doseNumber
        subject -> immunization.protocolApplied = create('Immunization_AppliedProtocol') as protocolApplied then {
            subject -> protocolApplied.doseNumber = evaluate(subject, 1) "rule_protocol_applied_dosage_number";
            subject -> protocolApplied.seriesDoses = evaluate(subject, 1) "rule_protocol_applied_dosage_number";
        } "rule_protocol_applied_value";

        subject -> immunization.patient = create('Reference') as patientReference, patientReference.reference = evaluate(subject, $this.id.replaceMatches('/_history/.*', '')) "rule_immunization_patient";
        subject -> immunization.occurrence = evaluate(questionnaireResponse, $this.item.where(linkId = '1e1f0f3b-58de-41b4-8db0-6c4b033cce57').answer.value.toString() + 'T00:00:00.00Z') "rule_immunization_occurrenceDateTime";

        // add MR to careplan irrespective of its validity
        subject then {
            subject -> evaluate(careplan, activity.where(detail.kind = 'MedicationRequest')) as activity, activity.outcomeReference = reference(immunization) "rule_careplan_task_reference";
             subject -> careplan.contained = immunization "rule_add_task";
        } "rule_careplan_task";
    } "rule_immunization";
}


group updateMedicationRequest (
    source period: Period,
    source version: Integer,
    source subject : Patient,
    source definition: ActivityDefinition,
    source questionnaireResponse: QuestionnaireResponse,
    target careplan: CarePlan) {

    subject -> create('MedicationRequest') as medicationRequestOrder then {
        subject -> medicationRequestOrder.id =  create('id') as id then {
                subject -> id.value = evaluate(questionnaireResponse, $this.item.where(linkId = 'c1d8fed1-ff36-43b6-8061-22561154b13b').answer.value) "rule_update_medication_request_value";
            } "rule_update_medication_request_value_id";
        subject -> medicationRequestOrder.basedOn = reference(careplan) "rule_update_flag_basedOn";
        subject -> medicationRequestOrder.status = 'completed' "r_medication_request_status";
        subject -> medicationRequestOrder.intent = 'order' "r_medication_request_intent";

        // add MR to careplan irrespective of its validity
        subject then {
            subject -> evaluate(careplan, activity.where(detail.kind = 'MedicationRequest')) as activity, activity.outcomeReference = reference(medicationRequestOrder) "rule_careplan_task_reference";
            subject -> careplan.contained = medicationRequestOrder "rule_add_task";
        } "rule_careplan_task";
    } "r_medication_request";
}

group extractMedicationRequestPlanOnHold(
    source period: Period,
    source version: Integer,
    source subject : Patient,
    source definition: ActivityDefinition,
    source questionnaireResponse: QuestionnaireResponse,
    target careplan: CarePlan) {

    //or answer.where(value.code = 'DE166') or answer.where(value.code = 'DE167')
    subject -> create('MedicationRequest') as medicationRequestCancelledOrHold then {
        subject -> medicationRequestCancelledOrHold.id =  create('id') as id then {
            subject -> id.value = evaluate(questionnaireResponse, $this.item.where(linkId = 'c1d8fed1-ff36-43b6-8061-22561154b13b').answer.value) "rule_update_medication_request_value";
        } "rule_update_medication_request_value_id";
        subject -> medicationRequestCancelledOrHold.basedOn = reference(careplan) "rule_update_flag_basedOn";
        subject -> medicationRequestCancelledOrHold.status = 'on-hold' "r_medication_request_status";
        subject -> medicationRequestCancelledOrHold.intent = 'plan' "r_medication_request_intent";

        // add MR to careplan irrespective of its validity
        subject then {
            subject -> evaluate(careplan, activity.where(detail.kind = 'MedicationRequest')) as activity, activity.outcomeReference = reference(medicationRequestCancelledOrHold) "rule_careplan_task_reference";
            subject -> careplan.contained = medicationRequestCancelledOrHold "rule_add_task";
        } "rule_careplan_task";
    } "r_medication_request";
}