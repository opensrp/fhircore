def module = project.name == "quest" ? "Quest" : ""

task fhircoreJacocoReport(type: JacocoReport, dependsOn: [
        "test${module}DebugUnitTest",
        "connected${module}DebugAndroidTest"
]) {

    reports {
        xml.required = true
        html.required = true
    }

    def excludes = [
            // Android
            '**Constant*.*',
            '**/*$ViewBinder*.*',
            'android/**/*.*',
            '**/*Test*.*',
            '**/*$ViewInjector*.*',
            '**/BuildConfig.*',
            '**/*BR*.*',
            '**/Manifest*.*',
            '**/R.class',
            '**/R$*.class',
            // Kotlin (Sealed and Data classes)
            '**/*$Lambda$*.*',
            '**/*Companion*.*',
            '**/*$Result.*',
            '**/*$Result$*.*',
            // Data classes and enums without functions or methods
            'org/smartregister/fhircore/engine/configuration/app/*',
            'org/smartregister/fhircore/engine/configuration/geowidget/*',
            'org/smartregister/fhircore/engine/configuration/navigation/*',
            'org/smartregister/fhircore/engine/configuration/profile/*',
            'org/smartregister/fhircore/engine/configuration/register/*',
            'org/smartregister/fhircore/engine/configuration/report/measure/*',
            'org/smartregister/fhircore/engine/configuration/view/ButtonProperties.kt',
            'org/smartregister/fhircore/engine/configuration/view/CardViewProperties.kt',
            'org/smartregister/fhircore/engine/configuration/view/ColumnProperties.kt',
            'org/smartregister/fhircore/engine/configuration/view/CompoundTextProperties.kt',
            'org/smartregister/fhircore/engine/configuration/view/ListProperties.kt',
            'org/smartregister/fhircore/engine/configuration/view/PersonalDataProperties.kt',
            'org/smartregister/fhircore/engine/configuration/view/RowProperties.kt',
            'org/smartregister/fhircore/engine/configuration/view/ServiceCardProperties.kt',
            'org/smartregister/fhircore/engine/configuration/view/SpacerProperties.kt',
            'org/smartregister/fhircore/engine/data/remote/model/response/*',
            'org/smartregister/fhircore/engine/domain/model/ExtractedResource.kt',
            'org/smartregister/fhircore/engine/domain/model/FhirResourceConfigs.kt',
            'org/smartregister/fhircore/engine/domain/model/OverflowMenuItemConfig.kt',
            'org/smartregister/fhircore/engine/domain/model/RuleConfig.kt',
            'org/smartregister/fhircore/engine/domain/model/SearchFilters.kt',
            'org/smartregister/fhircore/engine/domain/model/ServiceMemberIcon.kt',
            'org/smartregister/fhircore/engine/domain/model/ServiceStatus.kt',
            'org/smartregister/fhircore/engine/domain/model/SnackBarMessageConfig.kt',
            'org/smartregister/fhircore/engine/domain/model/TopBarConfig.kt',
            'org/smartregister/fhircore/engine/domain/model/OverflowMenuItemConfig.kt',
            'org/smartregister/fhircore/engine/domain/model/OverflowMenuItemConfig.kt',
            'org/smartregister/fhircore/engine/domain/model/OverflowMenuItemConfig.kt',
            'org/smartregister/fhircore/geowidget/model/*',
            // DI (Dagger and Hilt)
            '**/*_MembersInjector.class',
            '**/Dagger*Component.class',
            '**/Dagger*Component$Builder.class',
            '**/Dagger*Subcomponent*.class',
            '**/*Subcomponent$Builder.class',
            '**/*Module_*Factory.class',
            '**/*_Factory*.*',
            '**/*Module*.*',
            '**/*Dagger*.*',
            '**/*Hilt*.*',
            '**/dagger/hilt/internal/*',
            '**/hilt_aggregated_deps/*',
            '**/di/*',
            '**/*Hilt*.*',
            // Data Binding
            '**/databinding/*',
            'org/hl7/fhir/*'

    ]

    def moduleVariant = project.name == "quest" ? "questDebug" : "debug"
    def javaDebugTree = fileTree(dir: "$project.buildDir/intermediates/javac/${moduleVariant}/classes/", excludes: excludes)
    def kotlinDebugTree = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/${moduleVariant}", excludes: excludes)
    def mainSrc = "$project.projectDir/src/main/java"
    def kotlinSrc = "$project.projectDir/src/main/kotlin"

    sourceDirectories.setFrom(files([mainSrc, kotlinSrc]))
    classDirectories.setFrom(files([javaDebugTree, kotlinDebugTree]))

    executionData.setFrom(
            fileTree(dir: project.buildDir, includes: [
                    "outputs/unit_test_code_coverage/${moduleVariant}UnitTest/test${module}DebugUnitTest.exec",
                    "outputs/code_coverage/${moduleVariant}AndroidTest/connected/**/*.ec"
            ])
    )
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}