map "http://hl7.org/fhir/StructureMap/PatientRegistration" = 'PatientRegistration'

uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireReponse" as source
uses "http://hl7.org/fhir/StructureDefinition/Bundle" as target

group PatientRegistration(source src : QuestionnaireResponse, target bundle: Bundle) {
    src -> bundle.type = 'collection' "r_bun_type";
    src -> bundle.entry as entry, entry.resource = create('Patient') as patient then
        ExtractPatient(src, patient), ExtractPregnancyCondition(src, bundle, patient) "r_bun_entries";
}

group ExtractPatient(source src : QuestionnaireResponse, target patient : Patient) {
    src -> patient.id = uuid() "r_p_id_1";

    src.item as item_bio where(linkId = 'bio') then {
       item_bio.item as item_name where(linkId = 'PR-name') -> patient.name = create('HumanName') as patientName then {
          src -> patientName.family = evaluate(item_name, $this.item.where(linkId = 'PR-name-family').answer.value) "r_p_fname";
          src -> patientName.given = evaluate(item_name, $this.item.where(linkId = 'PR-name-given').answer.value) "r_p_gname";
       } "r_p_name";

       item_bio.item as item_iden where(linkId = 'PR-identifier-info') -> patient.identifier = create('Identifier') as patientIdent then {
         src -> patientIdent.value = evaluate(item_iden, $this.item.where(linkId = 'PR-identifier-nid').answer.value) "r_p_iden_val";
       } "r_p_iden";

       src -> patient.gender = evaluate(item_bio, $this.item.where(linkId = 'PR-gender').answer.value.code) "r_p_gender";

       item_bio.item as item_dob where(linkId = 'PR-birth-date' and answer.count() > 0) -> patient.birthDate = evaluate(item_dob, $this.answer.value) "r_p_dob";
       item_bio.item as item_age where(linkId = 'PR-age' and answer.count() > 0) -> patient.birthDate = evaluate(item_age, today() - ($this.answer.value.toString() + " 'years'").toQuantity()) "r_p_age";

       item_bio.item as item_marital where(linkId = 'PR-marital-status') -> patient.maritalStatus = create('CodeableConcept') as mcc then {
          src -> mcc.coding = evaluate(item_marital, $this.answer.value) "r_p_martial_cod";
       } "r_p_marital";

       item_bio.item as item_tele where (linkId = 'PR-telecom') -> patient.telecom = create('ContactPoint') as patientContact then {
          src -> patientContact.value = evaluate(item_tele, $this.item.where(linkId = 'PR-telecom-value').answer.value) "r_p_tel_num";
          src -> patientContact.system = "phone" "r_p_tel_sys";
       } "r_p_tel";

       item_bio.item as item_addr where (linkId = 'PR-address') -> patient.address = create('Address') as patientAddress then {
          src -> patientAddress.city = evaluate(item_addr, $this.item.where(linkId = 'PR-address-city').answer.value) "r_p_add_cty";
          src -> patientAddress.country = evaluate(item_addr, $this.item.where(linkId = 'PR-address-country').answer.value) "r_p_add_cntry";
          src -> patientAddress.use = "home" "r_p_add_use";
          src -> patientAddress.type = "physical" "r_p_add_typ";
       } "r_p_addr";

       item_bio.item as item_eth where (linkId = 'PR-ethnicity-group') -> patient.extension = create('Extension') as patientEthnicity then {
         src -> patientEthnicity.url = evaluate(item_eth, $this.item.where(linkId = 'PR-ethnicity-authority').answer.value) "r_p_eth_url";
         src -> patientEthnicity.value = evaluate(item_eth, $this.item.where(linkId = 'PR-ethnicity').answer.value) "r_p_eth_val";
       } "r_p_eth";

       item_bio.item as item_occ where (linkId = 'PR-occupation-group') -> patient.extension = create('Extension') as patientOccupation then {
        src -> patientOccupation.url = evaluate(item_occ, $this.item.where(linkId = 'PR-occupation-authority').answer.value) "r_p_occ_url";
        src -> patientOccupation.value = evaluate(item_occ, $this.item.where(linkId = 'PR-occupation').answer.value) "r_p_occ_val";
       } "r_p_occ";

       src -> patient.active = evaluate(item_bio, $this.item.where(linkId = 'PR-active').answer.value) "r_p_act";
    };
}

group ExtractPregnancyCondition(source src : QuestionnaireResponse, target bundle : Bundle, source patient : Patient) {
    src.item as item_bio where(linkId = 'bio') then {
        item_bio.item as item_preg where(linkId = 'is_pregnant' and answer.count() > 0 and answer.value.code = '77386006') then {
        	src -> bundle.entry as entry, entry.resource = create('Condition') as cnd then {
    	    	src -> cnd.id = uuid() "r_c_id";
    	    	src -> cnd.recordedDate = evaluate(src, now()) "r_c_rec_date";
    	    	src -> cnd.clinicalStatus = cc("http://terminology.hl7.org/CodeSystem/condition-clinical", 'active') "r_c_clinical_st";
    	    	src -> cnd.verificationStatus = cc("http://terminology.hl7.org/CodeSystem/condition-ver-status", 'confirmed') "r_c_verif_st";
    	    	src -> cnd.subject = reference(patient) "r_c_sub";
    	    	src -> cnd.code = create('CodeableConcept') as ccPreg then {
                  src -> ccPreg.coding = evaluate(item_preg, $this.answer.value) "r_c_cod_cod";
                  src -> ccPreg.text = "Pregnant" "r_c_cod_text";
               } "r_c_code";
        	} "r_c";
    	} "r_c_preg";
    };
}