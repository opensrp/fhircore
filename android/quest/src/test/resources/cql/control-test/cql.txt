//Declare name and version of lib
library ControlTest version '1'

//FHIR as a data model and version of FHIR
using FHIR version '4.0.1'

//functions to help FHIR vs CQL primitives
include "FHIRHelpers" version '4.0.1' called FHIRHelpers

//System declarations  : Codable concept codes systems
codesystem "SNOMED": 'http://snomed.info/sct'

//Code used as identifers
code "g6pdControl1": '410680006' from "SNOMED" display 'Control-mode 1'
code "g6pdControl2": '405358009' from "SNOMED" display 'Control-mode 2'
code "invalidResult": '455371000124106' from "SNOMED" display 'Incorrect Result'
code "validResult": '280413001' from "SNOMED" display 'Correct Result'
code "deviceOperation": '15361003' from "SNOMED" display 'Device Operation'


define "QR" : Last([QuestionnaireResponse] H)

define "Control1Obs": "QR".item.where(linkId='result_type').answer.value.first() ~ "g6pdControl1"

define "Control2Obs": "QR".item.where(linkId='result_type').answer.value.first() ~  "g6pdControl2"

define "HaemoglobinObs": "QR".item.where(linkId='haemoglobin').answer.first().value

define "G6PDObs": "QR".item.where(linkId='g6pd').answer.first().value

define "G6PDTemp": decimal {value: "G6PDObs".value}
define "G6PDValue": ToString("G6PDTemp")
define "HaemoglobinTemp": decimal {value: "HaemoglobinObs".value}
define "HaemoglobinValue": ToString("HaemoglobinTemp")

define "G6PD Normal":
    if "Control1Obs" is not null and "G6PDObs" between 0.0 and 3.0
    	then true
    else if "Control2Obs" is not null and "G6PDObs" between 6.0 and 10.0
    	then true
    else false

define "G6PD Conclusion":
    if "Control1Obs" is not null and "G6PD Normal"
    	then 'Value ('+ "G6PDValue"+') is in Normal G6PD Range 0-3'
    else if "Control2Obs" is not null and "G6PD Normal"
    	then 'Value ('+ "G6PDValue"+') is in Normal G6PD Range 6-12'
    else 'Value('+"G6PDValue"+') is Non Deterministic G6PD result'

define "Haemoglobin Normal":
    if "Control1Obs" is not null and "HaemoglobinObs" between 8.0 and 12.0
    	then true
    else if "Control2Obs" is not null and "HaemoglobinObs" between 13.0 and 17.0
    	then true
    else false

define "Haemoglobin Conclusion":
    if "Control1Obs" is not null and "Haemoglobin Normal"
    	then 'Value ('+ "HaemoglobinValue"+') is in Normal Haemoglobin Range 8-12'
    else if "Control2Obs" is not null and "Haemoglobin Normal"
    	then 'Value ('+ "HaemoglobinValue"+') is in Normal Haemoglobin Range 13-17'
    else 'Value('+"HaemoglobinValue"+') is Non Deterministic Haemoglobin result'

define "ConclusionCode":
  if "G6PD Normal" and "Haemoglobin Normal" then "validResult"
  else "invalidResult"

define "Conclusion": "ConclusionCode".display

define "Conclusion Details": '\nDetails:\n'+ "G6PD Conclusion" + '\n' + "Haemoglobin Conclusion"

define "QRRef": ReplaceMatches('QuestionnaireResponse/'+"QR".id,'#','')

define "Observation": Observation{
  focus: { Reference { reference: string { value: "QRRef" } } },
  code: CreateConcept("deviceOperation"),
  effective: dateTime{ value: Now() },
  value: CreateConcept("ConclusionCode")
}

define "OUTPUT": List { "Conclusion" , "Conclusion Details", "Observation"}

// helper functions
define function "CreateConcept"(code Code):
  CodeableConcept { coding: {
        Coding {
          code: code {value: "code".code },
          system: uri {value: "code".system },
          display: string {value: "code".display }
        }
  } }