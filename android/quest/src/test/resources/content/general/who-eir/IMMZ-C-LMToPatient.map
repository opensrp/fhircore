map "http://fhir.org/guides/who/smart-immunization/StructureMap/IMMZ-C-LMToPatient" = "IMMZ-C-LMToPatient"

uses "http://fhir.org/guides/who/smart-immunization/StructureDefinition/IMMZ-C-register-client" alias IMMZC as target
uses "http://hl7.org/fhir/StructureDefinition/Patient" alias Patient as target

group IMMZCToPatient (
  source immzc : IMMZC,
  target patient: Patient
) {

  immzc.uniqueId as id -> patient.identifier as identifier then {
    id -> identifier.value = id "set identifier value";
  } "set identifier";
  
  immzc -> patient.name as pname then NameToHumanName( immzc, pname ) "set name";

  immzc.sex as sex -> patient.gender = translate(sex, 'http://fhir.org/guides/who/smart-immunization/ConceptMap/IMMZ.C.SexToAdministrativeGender', 'code') "set gender";

  immzc.birthDate as birthDate -> patient.birthDate = birthDate "set birthDate";

  immzc.caregiver as caregiver -> patient.contact as contact, contact.name as hname 
    then NameToHumanName( caregiver, hname ) "set caregiver";

  immzc.phone as phone -> patient.telecom as telecom then {
    phone as content -> telecom.value = content, telecom.system = 'phone' "set phone value";
  } "set phone";

  immzc.administrativeArea as area -> patient.address as address then {
    area.coding first as coding -> address then {
      coding.code as content -> address.text = content "set address to code";
      coding.display as content -> address.text = content "set address to display";
    } "set address text";
  } "set administrativeArea";


}

group NameToHumanName (
  source name,
  target hname
) {
  name.name as fullName -> hname.text = fullName "set full name";
  name.firstName as firstName -> hname.given = firstName "set first name";
  name.familyName as familyName -> hname.family = familyName "set family name";
}