map "https://fhir.labs.smartregister.org/fhir/StructureMap/8f7828f5-3910-4bfc-94a9-a0da749fb37c" = "Physical Inventory Count and Stock Supply"

uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireResponse" as source
uses "http://hl7.org/fhir/StructureDefinition/Bundle" as target

group physicalInventoryCountStock(source questionnaireResponse : QuestionnaireResponse, target bundle: Bundle) {
    questionnaireResponse -> bundle.id = uuid() "rule_bundle_id";
    questionnaireResponse -> bundle.type = 'collection' "rule_bundle_type";
    questionnaireResponse -> evaluate(questionnaireResponse, $this.subject) as commodityGroupReference then extractEncounter(questionnaireResponse, commodityGroupReference, bundle) "rule_bundle_entries";
}

group extractEncounter(source questionnaireResponse : QuestionnaireResponse, source commodityGroupReference : Reference, target bundle : Bundle) {
    questionnaireResponse -> bundle.entry as entry, entry.resource = create('Encounter') as encounter then {
        questionnaireResponse -> encounter.id = uuid() "rule_encounter_id";
        questionnaireResponse -> encounter.status = 'finished' "rule_encounter_st";
        questionnaireResponse -> encounter.class = c("http://terminology.hl7.org/CodeSystem/v3-ActCode", "restock", "Physical Inventory Count and Stock Supply") "rule_encounter_cls";

        questionnaireResponse -> encounter.type = create('CodeableConcept') as concept then {
            questionnaireResponse -> concept.coding = c("http://snomed.info/sct", "184048005") as coding then {
                questionnaireResponse -> coding.display = 'Physical Inventory Count and Stock Supply' "rule_encounter_cod_disp";
            } "rule_encounter_cc_cod";
            questionnaireResponse -> concept.text = 'Physical Inventory Count and Stock Supply' "rule_encounter_typ_text";
        } "rule_encounter_typ";

        questionnaireResponse -> encounter.priority = create('CodeableConcept') as concept then {
            questionnaireResponse -> concept.coding = c("http://terminology.hl7.org/ValueSet/v3-ActPriority", "EL") as coding then {
                questionnaireResponse -> coding.display = 'elective' "rule_encounter_cod_disp";
            } "rule_encounter_cc_cod";
            questionnaireResponse -> concept.text = 'elective' "rule_encounter_prio_text";
        } "rule_encounter_prio";

        questionnaireResponse.subject as subject -> encounter.subject = subject "rule_encounter_sub";
        questionnaireResponse -> encounter.period = create('Period') as enPeriod then {
            questionnaireResponse -> enPeriod.start = evaluate(questionnaireResponse, now()) "rule_encounter_per_start";
            questionnaireResponse -> enPeriod.end = evaluate(questionnaireResponse, now()) "rule_encounter_perule_encounterd";
        } "rule_encounter_per";

        questionnaireResponse -> encounter.reasonCode = create('CodeableConcept') as concept then {
            questionnaireResponse -> concept.text = 'Physical Inventory Count and Stock Supply' "rule_encounter_text";
        } "rule_encounter_reason";

        // Adding the system stock balance before restock quantity component.
        questionnaireResponse.item as systemBalanceBeforeRestock where(linkId ='d2cda03f-dc14-4f15-85ab-d5c33027aa31' and answer.value.count() > 0) then {
            questionnaireResponse -> cc('http://snomed.info/sct','snapshot', 'Snapshot')  as balanceBeforeRestockObservation then {
                questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = 'd2cda03f-dc14-4f15-85ab-d5c33027aa31').answer.value) as balanceBeforeRestockQuantity then {
                    questionnaireResponse -> evaluate(questionnaireResponse, 'balance-before-restock') as codeText
                    then extractObservation(questionnaireResponse, commodityGroupReference, encounter, balanceBeforeRestockQuantity, balanceBeforeRestockObservation, codeText, bundle) "rule_observation_extraction";
                } "rule_current_stock_balance_component";
            } "rule_current_stock_balance_observation_component";
        } "rule_check_current_stock_balance_component";

        // Adding the stock on hand after physical count quantity component.
        questionnaireResponse.item as physicalCountStockOnHand where(linkId ='a71d3a65-9f08-4316-8ebf-41c376a4932e' and answer.value.count() > 0) then {
            questionnaireResponse -> cc('http://snomed.info/sct','snapshot', 'Snapshot')  as physicalCountStockOnHandObservation then {
                questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = 'a71d3a65-9f08-4316-8ebf-41c376a4932e').answer.value) as physicalCountStockOnHandQuantity then {
                    questionnaireResponse -> evaluate(questionnaireResponse, 'physical-count-soh') as codeText then extractObservation(questionnaireResponse, commodityGroupReference, encounter, physicalCountStockOnHandQuantity, physicalCountStockOnHandObservation, codeText, bundle) "rule_observation_extraction";
                } "rule_physical_count_soh_component";
            } "rule_physical_count_soh_observation_component";
        } "rule_check_physical_count_soh_component";

        // Adding the restocked quantity
        questionnaireResponse.item as  valueCategory where(linkId ='35f0cdc3-7c3f-4a7a-8299-62fd801b9510' and answer.value.count() > 0) then {
            questionnaireResponse -> cc('http://hl7.org/fhir/inventoryreport-counttype', 'addition', 'Addition') as restockedValueObservation then {
                questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = '35f0cdc3-7c3f-4a7a-8299-62fd801b9510').answer.value) as quantityRestocked then {
                        questionnaireResponse -> evaluate(questionnaireResponse, 'restocked') as codeText, evaluate(questionnaireResponse, quantityRestocked) as quantityRestockedQuantity then extractObservation(questionnaireResponse, commodityGroupReference, encounter, quantityRestockedQuantity, restockedValueObservation, codeText, bundle) "rule_observation_extraction";
                } "rule_restocked_quantity";
            } "rule_restocked_observation_component";
        } "rule_check_restocked_category";

        // Adding the expired stock quantity component.
        questionnaireResponse.item as  expiredStock  where(linkId ='cc3a573f-8faf-49bd-8649-c74fcd31ab7e' and answer.value.exists() and answer.value.toInteger() > 0) then {
            questionnaireResponse -> cc('http://snomed.info/sct','subtraction', 'Subtraction') as expiredStockObservation then {
                questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = 'cc3a573f-8faf-49bd-8649-c74fcd31ab7e').answer.value) as expiredStockQuantity then {
                    questionnaireResponse -> evaluate(questionnaireResponse, 'expiry') as codeText then extractObservation(questionnaireResponse, commodityGroupReference, encounter, expiredStockQuantity, expiredStockObservation, codeText, bundle) "rule_observation_extraction";
                } "rule_expired_stock_component";
            } "rule_expired_stock_observation_component";
        } "rule_check_expired_stock_component";

        // Adding the damaged stock quantity component.
        questionnaireResponse.item as damagedStock where(linkId ='4571fa5f-8448-4e69-8395-c1962154e39c' and answer.value.exists() and answer.value.toInteger() > 0) then {
            questionnaireResponse -> cc('http://snomed.info/sct','subtraction', 'Subtraction') as damagedStockObservation then {
                questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = '4571fa5f-8448-4e69-8395-c1962154e39c').answer.value) as damagedStockQuantity then {
                    questionnaireResponse -> evaluate(questionnaireResponse, 'damage') as codeText then extractObservation(questionnaireResponse, commodityGroupReference, encounter, damagedStockQuantity, damagedStockObservation, codeText, bundle) "rule_observation_extraction";
                } "rule_damaged_stock_component";
            } "rule_damaged_stock_observation_component";
        } "rule_check_damaged_stock_component";

        // Adding the Donation quantity component.
        questionnaireResponse.item as donation where(linkId ='be137683-fda1-4fe3-86e7-39bbd3c87299' and answer.value.exists() and answer.value.toInteger() > 0) then {
            questionnaireResponse -> cc('http://snomed.info/sct','addition', 'Addition') as donationStockObservation then {
                questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = 'be137683-fda1-4fe3-86e7-39bbd3c87299').answer.value) as donationQuantity then {
                    questionnaireResponse -> evaluate(questionnaireResponse, 'donation') as codeText then extractObservation(questionnaireResponse, commodityGroupReference, encounter, donationQuantity, donationStockObservation, codeText, bundle) "rule_observation_extraction";
                } "rule_donation_component";
            } "rule_donation_observation_component";
        } "rule_check_donation_component";

        // Adding the Over Reporting quantity component.
        questionnaireResponse.item as overReporting where(linkId ='66e1ca2e-a46a-42e2-c397-a12c30b8525e' and answer.value.exists() and answer.value.toInteger() > 0) then {
            questionnaireResponse -> cc('http://snomed.info/sct','subtraction', 'Subtraction') as overReportingObservation then {
                questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = '66e1ca2e-a46a-42e2-c397-a12c30b8525e').answer.value) as overReportingQuantity then {
                    questionnaireResponse -> evaluate(questionnaireResponse, 'over-reporting') as codeText then extractObservation(questionnaireResponse, commodityGroupReference, encounter, overReportingQuantity, overReportingObservation, codeText, bundle) "rule_observation_extraction";
                } "rule_over_reporting_component";
            } "rule_over_reporting_observation_component";
        } "rule_check_over_reporting_component";

        // Adding the Under Reporting quantity component.
        questionnaireResponse.item as underReporting where(linkId ='605f0de5-3d7e-4e3d-977b-b05932602d9e' and answer.value.exists() and answer.value.toInteger() > 0) then {
            questionnaireResponse -> cc('http://snomed.info/sct','addition', 'Addition') as underReportingObservation then {
                questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = '605f0de5-3d7e-4e3d-977b-b05932602d9e').answer.value) as underReportingQuantity then {
                    questionnaireResponse -> evaluate(questionnaireResponse, 'under-reporting') as codeText then extractObservation(questionnaireResponse, commodityGroupReference, encounter, underReportingQuantity, underReportingObservation, codeText, bundle) "rule_observation_extraction";
                } "rule_under_reporting_component";
            } "rule_under_reporting_observation_component";
        } "rule_check_under_reporting_component";

        //Adding the total balance after restock snapshot component
        questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = 'a71d3a65-9f08-4316-8ebf-41c376a4932e').answer.value) as stockOnHand,
        evaluate(questionnaireResponse, $this.item.where(linkId = '35f0cdc3-7c3f-4a7a-8299-62fd801b9510').answer.value) as quantityRestocked then {
            questionnaireResponse -> evaluate(questionnaireResponse, (stockOnHand + quantityRestocked)) as totalBalance then {

                //create the balance after restock observation
                questionnaireResponse -> cc('http://hl7.org/fhir/inventoryreport-counttype', 'snapshot', 'Snapshot') as totalBalanceObservation then {
                    questionnaireResponse -> evaluate(questionnaireResponse, 'balance-after-restock') as codeText then extractObservation(questionnaireResponse, commodityGroupReference, encounter, totalBalance, totalBalanceObservation, codeText, bundle) "rule_observation_extraction";
                } "rule_total_balance_component";

                //create flag if none exists and total balance is Zero
                questionnaireResponse as stockOut where (totalBalance = 0) then {
                    questionnaireResponse.item as noFlag where (linkId = '513d5111-449e-49e0-d43a-8953fb53ba7d' and answer.value.empty()) then {
                        questionnaireResponse then createStockOutFlag(questionnaireResponse, bundle, commodityGroupReference) "rule_create_stockout_flag";
                    } "rule_no_flag";
                } "rule_balance_is_zero";

                //close flag if it exits and total balance is more than zero
                questionnaireResponse as endStockOut where (totalBalance > 0) then {
                    questionnaireResponse.item as flagExists where (linkId = '513d5111-449e-49e0-d43a-8953fb53ba7d' and answer.value.exists()) then {
                        questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = '513d5111-449e-49e0-d43a-8953fb53ba7d').answer.value) as flagId then {
                            questionnaireResponse.item as flagStartPeriodCheck where (linkId = '6c5c6fd7-749c-402b-990b-df96a92bf6c8' and answer.value.exists()) then {
                                questionnaireResponse -> evaluate(questionnaireResponse, $this.item.where(linkId = '6c5c6fd7-749c-402b-990b-df96a92bf6c8').answer.value) as flagPeriodStart then updateStockOutFlag(questionnaireResponse, bundle, flagId, flagPeriodStart) "rule_update_stockout_flag";
                            } "rule_flag_start_date_check";
                        } "rule_flag_Id";
                    } "rule_flag_exists";
                } "rule_balance_is_greater_than_zero";

            } "rule_total_after_restock";
        } "rule_stock_on_hand_and_quantity_restocked";

        //Update the current latest observation if it exists
        questionnaireResponse.item as latestObservation where(linkId ='d6c39388-fb56-4e1f-8cfc-f199f2c89919' and answer.value.exists()) then {
            questionnaireResponse ->  evaluate(questionnaireResponse, $this.item.where(linkId = 'd6c39388-fb56-4e1f-8cfc-f199f2c89919').answer.value) as latestObservationId then updateLatestObservation(questionnaireResponse, bundle, latestObservationId)  "rule_update_current_latest_observation";
        } "rule_check_latest_observation";

    } "rule_encounter";
}

group extractObservation(source questionnaireResponse : QuestionnaireResponse, source commodityGroupReference : Reference, source encounter : Encounter, source valueQuantity : Integer, source categoryCodeableConcept  : CodeableConcept, source codeString: String, target bundle : Bundle) {
    questionnaireResponse -> bundle.entry as entry, entry.resource = create('Observation') as observation then {
        questionnaireResponse -> observation.id = uuid() "rule_observation_id";
        questionnaireResponse -> observation.status = 'final' "rule_observation_status";
        questionnaireResponse -> observation.category = cc('http://snomed.info/sct','386452003', 'Supply management') "rule_observation_supply_chain";
        questionnaireResponse -> observation.category = categoryCodeableConcept "rule_observation_type";
        questionnaireResponse -> observation.code = create('CodeableConcept') as code then {
            questionnaireResponse -> code.coding = create('Coding') as coding then {
                questionnaireResponse -> coding.system = 'https://smartregister.org/' "rule_observation_c_coding_system";
                questionnaireResponse -> coding.code = codeString "rule_observation_c_coding_code";
                questionnaireResponse -> coding.display = codeString "rule_observation_c_coding_display";
            } "rule_observation_c_coding";
            questionnaireResponse -> code.text = codeString "rule_observation_c_text";
        } "rule_observation_code";
		questionnaireResponse -> observation.subject = commodityGroupReference "r_reference_questionnaireResponse";
        questionnaireResponse -> observation.effective = evaluate(questionnaireResponse, now()) "r_effective_current_date_time";
        questionnaireResponse -> observation.performer = evaluate(questionnaireResponse, $this.generalPractitioner.first()) "rule_observation_performer";

        questionnaireResponse -> observation.value = create('Quantity') as quantity then {
			questionnaireResponse -> quantity.value = valueQuantity "rule_observation_quantity";
		} "rule_observation_value";

        questionnaireResponse -> observation.encounter = create('Reference') as encounterReference then {
          questionnaireResponse -> encounterReference.reference = evaluate(questionnaireResponse, 'Encounter/' + encounter.id) "rule_encounter_ref";
        } "rule_encounter";

        //Include component with running total for balance-after-restock snapshot observation and status 'latest'
        questionnaireResponse as runningTotal where (codeString = 'balance-after-restock') then {

            //status 'latest' for observation with current running balance
            questionnaireResponse -> observation.status = 'preliminary' "rule_observation_status_latest";

            // Observation component with running total
            questionnaireResponse -> observation.component as component then {
                questionnaireResponse -> component.code = create('CodeableConcept') as runningTotalCC then {
                    questionnaireResponse -> runningTotalCC.coding = create('Coding') as runningTotalCCCoding then {
                        questionnaireResponse -> runningTotalCCCoding.system = 'http://snomed.info/sct' "rule_observation_runing_total_cc_coding_system";
                        questionnaireResponse -> runningTotalCCCoding.code = '255619001' "rule_observation_runing_total_cc_coding_code";
                        questionnaireResponse -> runningTotalCCCoding.display = 'Total' "rule_observation_runing_total_cc_coding_display";
                    } "rule_observation_running_total_cc_coding";
                    questionnaireResponse -> runningTotalCC.text = 'Running total/Cumulative sum' "rule_observation_runing_total_cc_text";
                } "rule_observation_running_total_code";

                questionnaireResponse -> component.value = create('Quantity') as runningTotalQuantity then {
                    questionnaireResponse -> runningTotalQuantity.value = valueQuantity "rule_observation_quantity";
                } "rule_observation_running_total_quantity";
            } "rule_observation_running_total_component";

        } "rule_observation_running_total";

        //status 'final' for the other observations
        questionnaireResponse as otherObservations where (codeString != 'balance-after-restock') then {
            questionnaireResponse -> observation.status = 'final' "rule_observation_status_final";
        } "rule_observation_status";

    } "rule_observation";
}

group updateLatestObservation(source questionnaireResponse: QuestionnaireResponse, target bundle : Bundle, source latestObservationId : String){
    questionnaireResponse -> bundle.entry as entry, entry.resource = create('Observation') as observation then {
        questionnaireResponse -> observation.id = create('id') as id then {
            questionnaireResponse -> id.value = latestObservationId "rule_update_observation_id_value";
        } "rule_update_observation_id";
        questionnaireResponse -> observation.status = 'final' "rule_update_latest_observation_to_final";
        questionnaireResponse -> observation.effective = evaluate(questionnaireResponse, now()) "r_effective_current_date_time";
    } "rule__update_latest_observation";
}

group createStockOutFlag(source questionnaireResponse: QuestionnaireResponse, target bundle : Bundle, source commodityGroupReference: Reference){
    questionnaireResponse -> bundle.entry as entry, entry.resource = create('Flag') as flag then {
        questionnaireResponse -> flag.id = uuid() "rule_flag_id";
        questionnaireResponse -> flag.status = 'active' "rule_flag_status";
        questionnaireResponse -> flag.category = cc('http://snomed.info/sct','386452003', 'Supply management') "rule_flag_category";
        questionnaireResponse -> flag.code = cc('http://snomed.info/sct','419182006', ' Supplies not available') "rule_flag_code";
        questionnaireResponse -> flag.subject = commodityGroupReference "rule_flag_subject";
        questionnaireResponse -> flag.period = create('Period') as period then {
            questionnaireResponse -> period.start = evaluate(questionnaireResponse, now()) "rule_flag_period_start";
        } "rule_flag_period";
    } "rule_flag";
}

group updateStockOutFlag(source questionnaireResponse: QuestionnaireResponse, target bundle : Bundle, source flagId: String, source flagPeriodStart: DateTime){
    questionnaireResponse -> bundle.entry as entry, entry.resource = create('Flag') as flag then {
        questionnaireResponse -> flag.id = create('id') as id then {
            questionnaireResponse -> id.value = flagId "rule_update_flag_id_value";
         } "rule_update_flag_id";
        questionnaireResponse -> flag.status = 'inactive' "rule_update_flag_status_to_inactive";
        questionnaireResponse -> flag.period = create('Period') as period then {
            questionnaireResponse -> period.start = create('dateTime') as startDate then {
                questionnaireResponse -> startDate.value = flagPeriodStart "rule_update_flag_period_start_value";
            } "rule_update_flag_period_start";
            questionnaireResponse -> period.end = evaluate(questionnaireResponse, now()) "rule_update_flag_period_";
        } "rule_update_flag_period";
    } "rule_update_flag";
}