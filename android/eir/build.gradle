plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'de.mannodermaus.android-junit5'
    id 'org.jetbrains.dokka'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'jacoco'
    id 'dagger.hilt.android.plugin'
}

apply from: '../jacoco.gradle'
apply from: '../properties.gradle'

android {

    compileSdkVersion sdk_versions.compile_sdk

    lintOptions {
        abortOnError false
    }
    buildFeatures {
        viewBinding true
    }
    dataBinding {
        enabled true
    }
    defaultConfig {
        applicationId "org.smartregister.fhircore.eir"
        minSdkVersion sdk_versions.min_sdk
        targetSdkVersion sdk_versions.target_sdk
        versionCode 1
        versionName "0.0.10"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // Required when setting minSdkVersion to 20 or lower
        // See https://developer.android.com/studio/write/java8-support
        multiDexEnabled true

        buildConfigField("boolean", 'SKIP_AUTH_CHECK', "false")

        buildConfigField("String", 'FHIR_BASE_URL', "\"${FHIR_BASE_URL}\"")
        buildConfigField("String", 'OAUTH_BASE_URL', "\"${OAUTH_BASE_URL}\"")
        buildConfigField("String", 'OAUTH_CIENT_ID', "\"${OAUTH_CIENT_ID}\"")
        buildConfigField("String", 'OAUTH_CLIENT_SECRET', "\"${OAUTH_CLIENT_SECRET}\"")
        buildConfigField("String", 'OAUTH_SCOPE', "\"${OAUTH_SCOPE}\"")
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    packagingOptions {
        exclude 'license.html'
        exclude 'readme.html'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/license.html'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE.md'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/ASL-2.0.txt'
        exclude 'META-INF/LGPL-3.0.txt'
        exclude 'META-INF/sun-jaxb.episode'
        exclude("META-INF/*.kotlin_module")
    }
    // See https://developer.android.com/studio/write/java8-support
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs = ['-Xjvm-default=compatibility']
    }

    // avoid errors with message 'Duplicate files copied in APK ...'
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/*'
    }

    buildFeatures {
        compose true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.1.1'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                beforeTest { testDescriptor ->
                    println "${testDescriptor.className} > ${testDescriptor.name} STARTED"
                }
            }
        }
    }
}

configurations {
    all {
        exclude module: 'json'
        exclude module: 'xpp3'
    }
    //CQL
    all*.exclude group: 'javax.activation', module: 'activation'
    all*.exclude group: 'javax', module: 'javaee-api'
    all*.exclude group: 'xml-apis'
    all*.exclude group: 'org.opencds.cqf'
}

dependencies {
    implementation(project(":engine"))
   // implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    coreLibraryDesugaring deps.desugar
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'

    //Hilt - Dependency Injection
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    implementation deps.appcompat
    implementation deps.constraint_layout
    implementation deps.kotlin.stdlib
    implementation deps.lifecycle.extensions
    implementation deps.lifecycle.livedata_ktx
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.viewmodel
    implementation deps.material
    implementation deps.recyclerview

    implementation deps.core
    implementation deps.fragment

    implementation(deps.cql_engine.fhir) {
        transitive = false
    }
    implementation(deps.caffeine) {
        transitive = false
    }
    implementation(deps.guava) {
        transitive = false
    }
    implementation(deps.room.ktx) {
        transitive = false
    }
    implementation(deps.room.runtime) {
        transitive = false
    }
    implementation(deps.json_tools.json_patch) {
        transitive = false
    }

    api(deps.cql_engine.core) {
        transitive = false
    }
    api(deps.hapi_r4) {
        transitive = false
    }

    kapt deps.room.compiler

    testImplementation deps.junit5_api
    testRuntimeOnly deps.junit5_engine
    testRuntimeOnly deps.junit5_engine_vintage
    testImplementation deps.robolectric
    testImplementation deps.atsl.ext_junit
    testImplementation deps.atsl.ext_junit_ktx
    testImplementation deps.coroutines.test
    testImplementation deps.androidx.core_test
    debugImplementation deps.fragment_testing
    releaseImplementation deps.fragment_testing
    testImplementation deps.mockk
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0'

    androidTestImplementation deps.atsl.ext_junit
    androidTestImplementation deps.atsl.espresso

    def composeVersion = "1.1.1"
    debugImplementation("androidx.compose.ui:ui-test-manifest:$composeVersion")
    testImplementation("androidx.compose.ui:ui-test-junit4:$composeVersion")

    // Hilt test dependencies
    testImplementation("com.google.dagger:hilt-android-testing:$hiltVersion")
    kaptTest("com.google.dagger:hilt-android-compiler:$hiltVersion")
}

dokkaHtml.configure {
    outputDirectory.set(file("$rootDir/docs"))
    dokkaSourceSets {
        named("main") {
            moduleName.set("OpenSRP FHIR Core Documentation")
            jdkVersion.set(8)
            noAndroidSdkLink.set(false)
            perPackageOption {
                prefix = matching(".*\\.internal.*")
                suppress = true
            }
            suppressGeneratedFiles.set(true)
            skipEmptyPackages.set(true)
            reportUndocumented.set(true)
        }

        pluginsMapConfiguration.set(
                ["org.jetbrains.dokka.base.DokkaBase": """{
                "footerMessage": "Â© ${Year.now().value} Copyright Ona"
                }"""]
        )
    }
}
