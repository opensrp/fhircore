// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'deps.gradle'
    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31'
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.5.31"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.38.1'
        classpath deps.spotless
        classpath deps.kotlin_coveralls_plugin
        classpath deps.junit5_plugin
        classpath deps.dokka_plugin
        classpath "com.android.tools.build:gradle:7.0.4"
    }
}

allprojects {
    repositories {
        mavenLocal()
        google() 
        mavenCentral()

        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            name = "fhirsdk"
            url = uri("/Users/ndegwamartin/.m2.dev/fhirsdk")
        }
    }
 }

subprojects {
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'kotlin-kapt'

    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint).userData(['indent_size': '2', 'continuation_indent_size': '2'])
            ktfmt().googleStyle()
            licenseHeaderFile "${project.rootProject.projectDir}/license-header.txt"
        }

        format 'xml', {
            target '**/*.xml'
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if ('org.jacoco' == details.requested.group) {
                    details.useVersion versions.jacoco_tool
                }
            }
        }
    }
}
