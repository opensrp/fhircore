// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'deps.gradle'
    apply from: 'configs.gradle'

    ext.composeVersion = "1.3.2"
    ext.navigationVersion = "2.5.3"

    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10'
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.7.10"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.42'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.5.3"
        classpath deps.spotless
        classpath deps.kotlin_coveralls_plugin
        classpath deps.junit5_plugin
        classpath "com.android.tools.build:gradle:7.1.3"
    }
}

plugins {
    id 'org.jetbrains.dokka' version "1.7.20"
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()

        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url 'https://jcenter.bintray.com/'
        }
    }
}

subprojects {
    apply {
        plugin('com.diffplug.spotless')
        plugin('org.jetbrains.dokka')
    }

    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint).userData(['indent_size': '2', 'continuation_indent_size': '2'])
            ktfmt().googleStyle()
            licenseHeaderFile "${project.rootProject.projectDir}/license-header.txt"
        }

        format 'xml', {
            target '**/*.xml'
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }

        format 'json', {
            target '**/*.json'
            indentWithSpaces(2)
            trimTrailingWhitespace()
        }
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if ('org.jacoco' == details.requested.group) {
                    details.useVersion versions.jacoco_tool
                }
            }
        }

        dokkaHtmlMultiModule {
            outputDirectory.set(file("${project.rootProject.projectDir}/docs"))
        }
    }
}
