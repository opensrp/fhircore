// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'deps.gradle'
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath deps.android_gradle_plugin
        classpath deps.kotlin.plugin
        classpath deps.spotless
        classpath deps.kotlin_coveralls_plugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            name = "AndroidLibForArtifactory"
            url = uri("https://maven.pkg.github.com/google/android-fhir")

            def githubUsername = System.getenv('FHIRCORE_USERNAME')
                    ?: System.getenv('GITHUB_USER')
                    ?: ""
            def githubPersonalAccessToken = System.getenv('FHIRCORE_ACCESS_TOKEN')
                    ?: System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN')
                    ?: ""

            Properties properties = new Properties()
            if (rootProject.file("local.properties").exists()) {
                properties.load(rootProject.file("local.properties").newDataInputStream())

                githubUsername = properties.getProperty("GITHUB_USER")
                githubPersonalAccessToken = properties.getProperty("GITHUB_PERSONAL_ACCESS_TOKEN")
            }

            credentials {
                username = githubUsername
                password = githubPersonalAccessToken
            }
        }
    }

    subprojects {
        apply plugin: 'com.diffplug.spotless'
        spotless {
            kotlin {
                target '**/*.kt'
                ktlint(versions.ktlint).userData(['indent_size': '2', 'continuation_indent_size': '2'])
                ktfmt().googleStyle()
                licenseHeaderFile "${project.rootProject.projectDir}/license-header.txt"
            }
        }
    }
}
